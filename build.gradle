plugins {
	id 'java'
	id 'maven-publish'
	id 'idea'
	id 'eclipse'
	id 'fabric-loom' version '0.2.6-SNAPSHOT'
	id 'com.matthewprenger.cursegradle' version "1.1.2"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = project.archive_name
version = project.mod_version
group = project.maven_group

def ENV = System.getenv()
version = "${version}-${project.minecraft_version}" + (forkAuthor ? "-${forkAuthor}" : ("+" + (ENV.BUILD_NUMBER ? ("build." + ENV.BUILD_NUMBER) : "custom")))

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"
	modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	//Added to fix it failing to compile, needs looking into some more.
	compileOnly "com.google.code.findbugs:jsr305:3.0.2"
}

minecraft {
	refmapName = 'modmenu.refmap.json'
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}

			afterEvaluate {
				artifact(remapJar) {
					builtBy remapJar
				}
			}
		}
	}

	repositories {
		mavenLocal()
		// You should define the values of 'publishUrl', 'publishUsername' and 'publishPassword' in a gradle.properties file.
		// See: https://docs.gradle.org/current/userguide/build_environment.html#sec:gradle_configuration_properties
		if (project.hasProperty('publishUrl') &&
			project.hasProperty('publishUsername') &&
			project.hasProperty('publishPassword')) {
			maven {
				url = "${publishUrl}"
				credentials {
					username = "${publishUsername}"
					password = "${publishPassword}"
				}
			}
		}
	}
}

curseforge {
	if (project.hasProperty('curse_api_key')){
		apiKey = project.getProperty('curse_api_key')
	}
	project {
		id = '308702'
		changelog = 'A changelog can be found at https://github.com/Prospector/ModMenu'
		releaseType = 'beta'
		addGameVersion '1.16-Snapshot'
		mainArtifact(remapJar) {
			displayName = "ModMenu v$version for $project.minecraft_version"
		}
		afterEvaluate {
			uploadTask.dependsOn("remapJar")
		}
	}
	options{
		forgeGradleIntegration = false
	}
}
